# Copyright 2021 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/target_types.gni")

declare_args() {
  # GPIO Port of LED (ex. "G")
  pw_board_led_stm32cubef4_gpio_port = ""

  # GPIO Pin of LED (ex. "13")
  pw_board_led_stm32cubef4_gpio_pin = ""
}

config("flags") {
  cflags = [
    "-DLED_PORT_CHAR=" + pw_board_led_stm32cubef4_gpio_port,
    "-DLED_PIN_NUM=" + pw_board_led_stm32cubef4_gpio_pin,
  ]
}

if (pw_board_led_stm32cubef4_gpio_port != "" &&
    pw_board_led_stm32cubef4_gpio_pin != "") {
  pw_source_set("pw_board_led_stm32cubef4") {
    public_configs = [ ":flags" ]
    deps = [
      "$dir_pw_board_led:pw_board_led.facade",
      "//third_party/stm32cubef4:stm32f4xx_hal",
    ]
    sources = [ "led.cc" ]
  }
}
