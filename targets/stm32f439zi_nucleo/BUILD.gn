# Copyright 2021 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/target_types.gni")
import("$dir_pw_docgen/docs.gni")
import("$dir_pw_malloc/backend.gni")
import("$dir_pw_third_party/freertos/freertos.gni")
import("$dir_pw_toolchain/generate_toolchain.gni")
import("target_toolchains.gni")

generate_toolchains("target_toolchains") {
  toolchains = pw_target_toolchain_stm32f439zi_nucleo_list
}

config("module_config_options") {
  cflags = [
    "-include",
    rebase_path("module_config_overrides.h", root_build_dir),
  ]
}

pw_source_set("module_config_overrides") {
  public_configs = [ ":module_config_options" ]
  sources = [ "module_config_overrides.h" ]
}

config("pw_malloc_active") {
  if (pw_malloc_BACKEND != "") {
    defines = [ "PW_MALLOC_ACTIVE=1" ]
  }
}

if (current_toolchain != default_toolchain) {
  pw_source_set("pre_init") {
    configs = [ ":pw_malloc_active" ]
    public_deps = [ "$dir_pw_sys_io_stm32cube" ]
    deps = [
      "$dir_pw_preprocessor",
      "$dir_pw_third_party/freertos",
      "$dir_pw_third_party/stm32cube",
    ]
    sources = [ "init.c" ]
  }

  config("config_includes") {
    include_dirs = [ "config" ]
  }

  pw_source_set("freertos_config") {
    public_configs = [ ":config_includes" ]
    public = [ "config/FreeRTOSConfig.h" ]
  }

  pw_source_set("stm32f4xx_hal_config") {
    public_configs = [ ":config_includes" ]
    public = [ "config/stm32f4xx_hal_conf.h" ]
  }
}

pw_doc_group("target_docs") {
  sources = [ "target_docs.rst" ]
}
