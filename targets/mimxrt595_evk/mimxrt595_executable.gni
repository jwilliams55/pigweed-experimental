# Copyright 2021 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/exec.gni")
import("$dir_pw_malloc/backend.gni")

# Executable wrapper that includes some baremetal startup code.
template("mimxrt595_executable") {
  # .elf binary created by the standard toolchain.
  _bin_name = "${target_name}.bin"
  _elf_name = "${target_name}.elf"

  executable(_elf_name) {
    forward_variables_from(invoker, "*")
    if (!defined(deps)) {
      deps = []
    }
    deps += [ "//targets/mimxrt595_evk:boot" ]
    if (pw_malloc_BACKEND != "") {
      if (!defined(configs)) {
        configs = []
      }
      configs += [ "$dir_pw_malloc:pw_malloc_wrapper_config" ]
    }
  }

  # .bin binary created by extracting from the toolchain output.
  pw_exec(_bin_name) {
    if (defined(invoker.output_dir)) {
      _output_dir = invoker.output_dir
    } else {
      _output_dir = target_out_dir
    }

    outputs = [ "${_output_dir}/${_bin_name}" ]
    deps = [ ":${_elf_name}" ]

    program = "arm-none-eabi-objcopy"
    args = [
      "-Obinary",
      "<TARGET_FILE(:${_elf_name})>",
      rebase_path(outputs[0], root_build_dir),
    ]
  }

  group(target_name) {
    deps = [ ":${_bin_name}" ]
  }
}
