# Copyright 2022 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")
import("$dir_pw_build/target_types.gni")
import("$dir_pw_unit_test/test.gni")

declare_args() {
  # Enable nearest-neighbor display resizing, during display update, if the
  # framebuffer size does not match the display driver size.
  pw_display_DISPLAY_RESIZE = "0"
}

config("public_includes") {
  include_dirs = [ "public" ]
}

config("build_config") {
  cflags = [ "-DDISPLAY_RESIZE=" + pw_display_DISPLAY_RESIZE ]
  visibility = [ ":*" ]
}

pw_source_set("pw_display") {
  public_configs = [
    ":public_includes",
    ":build_config",
  ]
  public = [ "public/pw_display/display.h" ]
  public_deps = [
    "$dir_pw_coordinates",
    "$dir_pw_display_driver:display_driver",
    "$dir_pw_framebuffer",
    "$dir_pw_framebuffer_pool",
    "$dir_pw_status",
  ]
  sources = [ "display.cc" ]
}

pw_test("display_test") {
  deps = [ ":pw_display" ]
  sources = [ "display_test.cc" ]
}

pw_test_group("tests") {
  tests = [ ":display_test" ]
}
